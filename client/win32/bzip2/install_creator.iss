; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Code]
function IsHDHRCInstalled: boolean;
begin
  result := FileExists('C:\Program Files\Silicondust\HDHomeRun\hdhomerun_config.exe');
end;

function InitializeSetup(): Boolean;
var 
  ErrCode: integer;
begin
  result := IsHDHRCInstalled;
  if not result then
    if MsgBox('hdhomerun_config.exe must be installed for this program to work.  It was not found in C:\Program Files\Silicondust\HDHomeRun\ See the README file if you already have it installed elsewhere.  Would you like to install it now?', mbConfirmation, MB_YESNO) = IDYES
    then begin
      ShellExecAsOriginalUser('open', 'https://download.silicondust.com/hdhomerun/hdhomerun_windows.exe',
        '', '', SW_SHOW, ewNoWait, ErrCode);
    end;
  Result := True;
end;

[Setup]
AppName=scan_tuner
AppVersion=2.0
WizardStyle=modern
DisableDirPage=no
DefaultDirName={sd}\TVDX
DefaultGroupName=TVDX
UninstallDisplayIcon={app}\scan_tuner.exe
Compression=lzma2
SolidCompression=yes
OutputDir=C:\tvdx_src\root\static
; "ArchitecturesAllowed=x64" specifies that Setup cannot run on
; anything but x64.
ArchitecturesAllowed=x64
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64
OutputBaseFilename=tvdx_install

[Files]
; scan_tuner.exe was created with 'pp -o scan_tuner.exe scan_tuner.pl
Source: "C:\tvdx_src\client\win32\bzip2\scan_tuner.exe"; DestDir: "{app}";
Source: "C:\tvdx_src\client\win32\bzip2\README.txt"; DestDir: "{app}"; Flags: isreadme

[Run]
Filename: "schtasks"; \
    Parameters: "/Create /F /SC ONSTART /TN ""TVDX scan"" /RU ""NT AUTHORITY\LOCALSERVICE"" /TR ""'{app}\scan_tuner.exe'"""; \
    Flags: runhidden; \
    StatusMsg: "Running schtasks to run scan_tuner on start up"
; schtasks does not have certain switches so change it with powershell
Filename: "powershell"; \
    Parameters: "-command ""$t=Get-ScheduledTask 'TVDX scan';$s=$t.Settings;$s.RestartInterval='PT5M';$s.RestartCount=9999;$s.ExecutionTimeLimit='PT0S';$s.DisallowStartIfOnBatteries=0;$s.StopIfGoingOnBatteries=0;Set-ScheduledTask $t;"; \
    Flags: runhidden; \
    StatusMsg: "Changing scheduled task execution time limit"
Filename: "schtasks"; \
    Parameters: "/Run /TN ""TVDX scan"""; \
    Flags: runhidden; \
    StatusMsg: "Starting scan_tuner.exe scheduled task"

[UninstallRun]
Filename: "schtasks"; \
    Parameters: "/End /TN ""TVDX scan"""; \
    Flags: runhidden; \
    StatusMsg: "Stopping scheduled task TVDX scan"
Filename: "schtasks"; \
    Parameters: "/Delete /F /TN ""TVDX scan"""; \
    Flags: runhidden; \
    StatusMsg: "Running schtasks to stop running scan_tuner.exe on start up"