; Script generated by the Inno Script Studio Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

[Setup]
AppName=scan_tuner
AppVersion=2.0
WizardStyle=modern
DefaultDirName={autopf}\TVDX
DefaultGroupName=TVDX
UninstallDisplayIcon={app}\scan_tuner.exe
Compression=lzma2
SolidCompression=yes
OutputDir=C:\tvdx_src\client\win32
; "ArchitecturesAllowed=x64" specifies that Setup cannot run on
; anything but x64.
ArchitecturesAllowed=x64
; "ArchitecturesInstallIn64BitMode=x64" requests that the install be
; done in "64-bit mode" on x64, meaning it should use the native
; 64-bit Program Files directory and the 64-bit view of the registry.
ArchitecturesInstallIn64BitMode=x64
OutputBaseFilename=tvdx_install

[Files]
Source: "C:\tvdx_src\client\win32\bzip2\scan_tuner.exe"; DestDir: "{app}";

[Run]
Filename: "schtasks"; \
    Parameters: "/Create /F /SC ONSTART /TN ""TVDX scan"" /RU ""NT AUTHORITY\LOCALSERVICE"" /TR ""'{app}\scan_tuner.exe'"""; \
    Flags: runhidden; \
    StatusMsg: "Running schtasks to run scan_tuner on start up"
; schtasks does not have certain switches so change it with powershell
Filename: "powershell"; \
    Parameters: "-command ""$task = Get-ScheduledTask 'TVDX scan';$task.Settings.RestartInterval = 'PT5M';$task.Settings.RestartCount = 9999;$task.Settings.ExecutionTimeLimit = 'PT0S';$task.Settings.DisallowStartIfOnBatteries = 0;$task.Settings.StopIfGoingOnBatteries = 0;Set-ScheduledTask $task;"; \
    Flags: runhidden; \
    StatusMsg: "Changing scheduled task execution time limit"

[UninstallRun]
Filename: "schtasks"; \
    Parameters: "/End /TN ""TVDX scan"""; \
    Flags: runhidden; \
    StatusMsg: "Stopping scheduled task TVDX scan"
Filename: "schtasks"; \
    Parameters: "/Delete /F /TN ""TVDX scan"""; \
    Flags: runhidden; \
    StatusMsg: "Running schtasks to stop running scan_tuner.exe on start up"